dnl Process this file with autoconf to produce a configure script.

AC_PREREQ(2.52)

AC_INIT([gnome-system-monitor], [2.19.4],
        [http://bugzilla.gnome.org/enter_bug.cgi?product=system-monitor])
AC_CONFIG_SRCDIR(configure.in)
AC_CONFIG_HEADERS(config.h)

AM_INIT_AUTOMAKE
AC_SUBST(ACLOCAL_AMFLAGS, [\${ACLOCAL_FLAGS}])

AM_PROG_LIBTOOL

IT_PROG_INTLTOOL([0.35.0])
PKG_PROG_PKG_CONFIG([0.19])

AC_ISC_POSIX
AC_PROG_CC
AC_LANG_CPLUSPLUS
AC_LANG_COMPILER_REQUIRE
AM_PROG_CC_STDC
AC_HEADER_STDC
GNOME_COMPILE_WARNINGS

GLIB_REQUIRED=2.9.1
GCONF_REQUIRED=1.1.5
LIBWNCK_REQUIRED=2.5.0
LIBGTOP_REQUIRED=2.19.3
GTK_REQUIRED=2.8.0
GNOME_VFS_REQUIRED=2.6.0
GNOME_ICON_THEME_REQUIRED=2.15.3
PCRECPP_REQUIRED=6.4
GTKMM_REQUIRED=2.8

PKG_CHECK_MODULES(PROCMAN, glib-2.0 >= $GLIB_REQUIRED gconf-2.0 >= $GCONF_REQUIRED libgtop-2.0 >= $LIBGTOP_REQUIRED libwnck-1.0 >= $LIBWNCK_REQUIRED gtk+-2.0 >= $GTK_REQUIRED gnome-vfs-2.0 >= $GNOME_VFS_REQUIRED gnome-icon-theme >= $GNOME_ICON_THEME_REQUIRED gtkmm-2.4 >= $GTKMM_REQUIRED)


AC_ARG_ENABLE([pcrecpp], AS_HELP_STRING([--enable-pcrecpp], [Enable pcrecpp. You really want this ! (but default: disabled)]), [enable_pcrecpp=$enableval], [enable_pcrecpp=no])

if test "x$enable_pcrecpp" = "xyes" ; then
   PKG_CHECK_MODULES(PCRECPP, libpcre >= $PCRECPP_REQUIRED)
   enable_pcrecpp=yes
   AC_DEFINE(HAVE_PCRECPP, 1, [Defined if you have pcrecpp])
   PCRECPP_LIBS="${PCRECPP_LIBS} -lpcrecpp"
else
   enable_pcrecpp=no
   PCRECPP_CFLAGS=
   PCRECPP_LIBS=
fi

AC_SUBST(PCRECPP_CFLAGS)
AC_SUBST(PCRECPP_LIBS)


AC_ARG_ENABLE(more-warnings,
[AC_HELP_STRING([--enable-more-warnings], [Maximum compiler warnings])],
set_more_warnings="$enableval",[
    set_more_warnings=yes
])
AC_MSG_CHECKING(for more warnings, including -Werror)
if test "$GCC" = "yes" -a "$set_more_warnings" != "no"; then
    AC_MSG_RESULT(yes)
    CFLAGS="\
    -Wall \
    -Winline \
    -Wchar-subscripts -Wmissing-declarations -Wmissing-prototypes \
    -Wnested-externs -Wpointer-arith \
    -Wcast-align -Wsign-compare \
    -std=gnu89 \
    $CFLAGS"
    CXXFLAGS="-Wall -std=c++98 $CXXFLAGS"
else
    AC_MSG_RESULT(no)
fi

dnl CXXFLAGS="-fvisibility=hidden -fvisibility-inlines-hidden $CXXFLAGS"
dnl CXXFLAGS="-fvisibility-inlines-hidden $CXXFLAGS"

AM_GCONF_SOURCE_2


GETTEXT_PACKAGE=gnome-system-monitor
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE,"$GETTEXT_PACKAGE",
                   [The gettext catalog name])
AM_GLIB_GNU_GETTEXT
GNOME_DOC_INIT

dnl Set PACKAGE_LOCALE_DIR in config.h.
GLIB_DEFINE_LOCALEDIR(PACKAGE_LOCALE_DIR)


AC_CONFIG_FILES([
Makefile
src/Makefile
pixmaps/Makefile
po/Makefile.in
help/Makefile
gnome-system-monitor.desktop.in
])

AC_OUTPUT

echo "

Configuration:

	Source code location:	${srcdir}
	C Compiler:		${CC}
	C++ Compiler:		${CXX}
	CFLAGS:			${CFLAGS}
	CXXFLAGS:		${CXXFLAGS}
	pcrecpp:		${enable_pcrecpp}
	Maintainer mode:	${USER_MAINTAINER_MODE}
	Languages:		${ALL_LINGUAS}
"
